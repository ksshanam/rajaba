apply plugin: 'application'
apply plugin: 'nebula-test-jar'

dependencies {
  compile project(':suro-core')
  compile project(':suro-client')
  compile project(':suro-kafka-producer')
  compile project(':suro-kafka-consumer')
  compile project(':suro-localfile')
  compile project(':suro-s3')
  compile project(':suro-elasticsearch')

  compile 'org.eclipse.jetty:jetty-servlet:7.6.7.v20120910'
  compile 'com.sun.jersey.contribs:jersey-guice:1.11'
  compile 'com.sun.jersey:jersey-bundle:1.11'
  compile 'javax.servlet:servlet-api:2.5'
  compile 'com.amazonaws:aws-java-sdk-sqs:1.9.3'
  compile 'commons-cli:commons-cli:1.2'
  
  testCompile 'junit:junit:4.11'
  testCompile 'org.mockito:mockito-core:1.9.5'
  testCompile 'org.apache.curator:curator-test:2.4.2'
  testCompile project(path: ':suro-client', configuration: 'test')
  testCompile project(path: ':suro-kafka-producer', configuration: 'test')
  testCompile project(path: ':suro-kafka-consumer', configuration: 'test')
}

mainClassName = "com.netflix.suro.SuroServer"
task (runServer, type: JavaExec, dependsOn: jar) {
  main = "com.netflix.suro.SuroServer"
  classpath = sourceSets.main.runtimeClasspath
  classpath += sourceSets.main.compileClasspath
  args = ['-m', 'conf/routingmap.json', '-s', 'conf/sink.json', '-i', 'conf/input.json', '-x', 'conf/extensions.json']
  systemProperty 'log4j.properties', 'conf/log4j.properties'
}
installApp {
  into('build/install/'+applicationName + '-' + version){
    from ('conf').into '/conf'
  }
}
distZip {
  into(applicationName){
    from ('conf').into applicationName + '-' + version + '/conf'
  } 
}
distTar {
  into(applicationName){
    from ('conf').into applicationName + '-' + version + '/conf'
  } 
}
